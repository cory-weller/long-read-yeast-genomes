
# module load snakemake
# test: snakemake -n -r
# interactive run: snakemake --cores 2
# cluster run: snakemake -j 10 --latency-wait 120 --cluster="sbatch -p quick"
# module load graphviz
# snakemake --dag | dot -Tsvg > dag.svg

# use the wildcards from bottom-up

configfile: "config.yaml"

REF = config['reference']
SAMPLES = config['samples'] # pacbio genomes
ALL_SAMPLES = [REF] + SAMPLES

localrules: all
rule all:
    input:
        expand("data/unique_orfs/{all_samples}_unique.fa", all_samples=ALL_SAMPLES) # fastq > pear

rule orffinder:
    # strip 70aa orfs from all genomes, including reference, dump in blastdb folder
    input: "data/genomes/{all_samples}.fasta"
    output: "data/blastdb/{all_samples}.fa"
    params:
        module="ORFfinder/0.4.3-sing-install",
        min_orf_len=config['min_orf_len']
    threads: 1
    shell:
        """
        module load {params.module}
        ORFfinder -outfmt=0 -ml {params.min_orf_len} -in {input} -out {output}
        """

rule blastdb:
    # creat blast db using orfs
    input: "data/blastdb/{all_samples}.fa"
    output: multiext("data/blastdb/{all_samples}.fa", ".pdb",".phr",".pin",".pjs",".pot",".psq",".ptf",".pto")
    params:
        module="blast/2.13.0+"
    threads: 1
    shell:
        """
        module load {params.module}
        makeblastdb -in {input} -dbtype prot
        """

rule blastp:
    # pull orfs from each blastdb and blast against the reference
    input:
        query="data/blastdb/{all_samples}.fa",
        check_db=multiext("data/blastdb/{all_samples}.fa", ".pdb",".phr",".pin",".pjs",".pot",".psq",".ptf",".pto"),
        db=f"data/blastdb/{REF}.fa"
    output: "data/blastp_ref/{all_samples}.tsv"
    params:
        module="blast/2.13.0+"
    threads: 4
    shell:
        """
        module load {params.module}
        blastp -num_threads {threads} -outfmt 6 -max_hsps=1 -db {input.db} -query {input.query} -out {output}
        """

rule unique_orfs:
    # python script to extract unique orfs from each blast search
    input:
        orfs="data/blastdb/{all_samples}.fa",
        blast_hits="data/blastp_ref/{all_samples}.tsv"
    output: "data/unique_orfs/{all_samples}_unique.fa"
    params:
        conda_env="alignparse-environment" # may not work
    threads: 1
    shell:
        """
        python scripts/unique_orfs.py -orfs {input.orfs} -blast {input.blast_hits} -out {output}
        """
